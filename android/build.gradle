plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = "com.relay.client"
    compileSdk = 33

    defaultConfig {
        minSdk = 24
        targetSdk = 33
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled = false
        }
    }

    packagingOptions {
        pickFirst "**/libjsc.so"
        pickFirst "**/libc++_shared.so"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'com.clevertree:jscbridge:1.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.yaml:snakeyaml:2.2'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.clevertree'
                artifactId = 'themed-styler-android'
                version = '1.2.9'

                pom {
                    name = 'Themed Styler for Android'
                    description = 'Native styling engine for Android'
                    url = 'https://github.com/clevertree/themed-styler'

                    licenses {
                        license {
                            name = 'MIT OR Apache-2.0'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'clevertree'
                            name = 'Ari Asulin'
                            email = 'ari.asulin@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/clevertree/themed-styler.git'
                        developerConnection = 'scm:git:ssh://github.com/clevertree/themed-styler.git'
                        url = 'https://github.com/clevertree/themed-styler'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                    password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
                }
            }
            mavenLocal()
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("publish") }
        def signingKey = project.findProperty("signing.secretKey")
        def signingPassword = project.findProperty("signing.password")
        if (signingKey) {
            useInMemoryPgpKeys(signingKey, signingPassword ?: "")
        }
        sign publishing.publications.release
    }
}
